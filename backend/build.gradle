plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'com.nakivo'
sourceCompatibility = 21
targetCompatibility = 21

// --- Versioning logic ---
def major = 1
def minor = 0
def buildNum = System.getenv("BUILD_NUMBER") ?: "SNAPSHOT"
def commit = System.getenv("GIT_COMMIT")?.take(7) ?: "local"

version = "${major}.${minor}.${buildNum}-${commit}"

bootJar {
    archiveFileName = "test-generator-full-${version}.jar"
    manifest {
        attributes 'Implementation-Version': version
    }
}

jar {
    enabled = false
}

// --- Custom tasks ---
tasks.register('printVersion') {
    doLast {
        println "Building version: ${version}"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'org.apache.poi:poi-ooxml:5.2.5'

    runtimeOnly 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}
